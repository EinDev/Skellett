import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Citizens
	maven {
		url "https://repo.citizensnpcs.co/"
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	//Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

	// Spigot
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.19.3-R0.1-SNAPSHOT')

	// Citizens
	implementation (group: 'net.citizensnpcs', name: 'citizensapi', version: '2.0.30-SNAPSHOT')

	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.6.3') {
		transitive = false
	}

	// JSON.org
	shadow (group: 'org.json', name: 'json', version: '20220320')

	implementation fileTree(dir: 'libraries', include: '*.jar')

}

tasks.register('setupAddonTest') {
	dependsOn shadowJar
	doLast {
		copy {
			from 'build/libs/Skellett-' + version + '.jar'
			into 'src/test/resources/runner_data/'
			rename('Skellett-' + version + '.jar', 'Skellett.jar')
		}
	}
}

// Create a test task with given name, environments dir/file, dev mode and java version.
void createTestTask(String name, String environments, boolean devMode, int javaVersion) {
	tasks.register(name, JavaExec) {
		dependsOn setupAddonTest
		javaLauncher = javaToolchains.launcherFor {
			languageVersion = JavaLanguageVersion.of(javaVersion)
		}
		if (devMode) {
			standardInput = System.in
		}
		group = 'execution'
		classpath = files([
		//	'build' + File.separator + 'libs' + File.separator + 'Skellett-' + version + '.jar',
		//	project.configurations.runtimeClasspath.find { it.name.startsWith('gson') },
			sourceSets.main.runtimeClasspath
		])
		main = 'ch.njol.skript.tests.platform.PlatformMain'
		args = [
			'build/test_runners',
			'src/test/resources/tests',
			'src/test/resources/runner_data',
			environments,
			devMode,
			false
		]
	}
}

def latestEnv = 'java17/paper-1.19.3.json'
def latestJava = 17
def oldestJava = 8

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ["-source", "" + oldestJava, "-target", "" + oldestJava]
}

// Register different Skript testing tasks
createTestTask('quickTest', 'src/test/resources/environments/' + latestEnv, false, latestJava)
createTestTask('addonTestJava17', 'src/test/resources/environments/java17', false, latestJava)
createTestTask('addonTestJava8', 'src/test/resources/environments/java8', false, oldestJava)
createTestTask('addonTestDev', 'src/test/resources/environments/' + (project.property('testEnv') == null
	? latestEnv : project.property('testEnv') + '.json'), true, Integer.parseInt(project.property('testEnvJavaVersion') == null
	? latestJava : project.property('testEnvJavaVersion')))
tasks.register('addonTest') {dependsOn addonTestJava8, addonTestJava17}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ("lang/") {
		include '*.lang'
		into 'lang/'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = version
	baseName = project.name
	classifier = ''
	minimize()
}
